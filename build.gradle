group "io.github.robhinds.elasticsearch-gradle"
version '0.0.1-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

apply plugin: 'groovy'
apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "de.gesellix:gradle-docker-plugin:2017-11-08T12-39-47"
    testCompile "junit:junit:4.12"
}

apply from: 'bintray.gradle'
